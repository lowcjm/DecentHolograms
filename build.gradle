subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    group = 'eu.decentsoftware.holograms'

    repositories {
        mavenCentral()
        maven { url = "https://repo.codemc.io/repository/nms/" }
    }

    dependencies {
        if (!project.path.endsWith(":shared")) {
            implementation project(":shared")
        }
    }

    java {
        withSourcesJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        options.encoding = "UTF-8"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId    = rootProject.findProperty("group") ?: project.group
                artifactId = project.name
                version    = rootProject.version
            }
        }
    }

    // Disable all test tasks globally
    tasks.withType(Test).configureEach {
        enabled = false
    }
}